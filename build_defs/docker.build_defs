def docker_image(
        name,
        srcs = [],
        version = "",
        dockerfile = "Dockerfile",
        base_image = "",
        labels = [],
        run_args = "",
        repo="",
        test_only = False,
        visibility = None):
    """docker_image defines a build rule for a Docker image.

    You must use `plz run` to actually build the target.
    There are also several related targets created, for example a fully qualified name
    (henceforth fqn) which is used by other rules to identify this one.

    Args:
      name: Name of the rule.
      srcs: Source files that are available within the dockerfile.
      image: Name of the image to create.
      version: Version to tag the image with. If not given, a hash will be derived for the version.
      dockerfile: The Dockerfile that defines how to build this image.
      base_image: The build target that defines the base image. Only necessary if this image
                  is based off another plz image.
      labels: Labels to tag this rule with.
      run_args: Any additional arguments to provide to 'docker run'.
      test_only: If True, this can only be depended on by test rules.
      visibility: Visibility of this rule.
    """
    image = package_name() + "/" + name
    if base_image:
        base_image = canonicalise(base_image)
    labels += ["docker_image=" + image, "docker"]

    # This tarball becomes the build context for Docker.
    tarball, dockerfile = _docker_tarball_rule(
        name = name,
        srcs = srcs,
        dockerfile = dockerfile,
        base_image = base_image,
        test_only = test_only,
    )

    # The FQN defines a unique hash for the image.
    version = version or '$(echo $(hash %s) | md5sum - | cut -f1 -d" ")' % tarball
    fqn = build_rule(
        name = "%s_fqn" % name,
        srcs = [tarball],
        cmd = 'echo -n "%s/%s:%s" >> $OUT' % (repo, image, version),

        outs = ["%s_fqn" % name],
        deps = ["%s_fqn" % base_image if base_image else None],
        labels = labels,
        stamp = True,
        visibility = visibility,
        test_only = test_only,
    )

    # docker build
    cmd = "docker build -t `cat $SRCS_FQN` -f $(basename $SRCS_DOCKERFILE) - < $(out_location %s)" % tarball
    if base_image:
        cmd = "./$(out_location %s) && %s" % (base_image, cmd)
    docker_build = sh_cmd(
        name = name,
        cmd = cmd,
        srcs = {
            "fqn": [fqn],
            "dockerfile": [dockerfile],
        },
        deps = [
            base_image,
            tarball,
        ],
        visibility = visibility,
        labels = labels + ["docker-build"],
    )

    # For compat reasons _load is an alias for the main build target.
    filegroup(
        name = name + "_load",
        srcs = [docker_build],
        binary = True,
        test_only = test_only,
    )

    # docker run
    sh_cmd(
        name = name + "_run",
        cmd = "./$(out_location %s) && docker run -it %s `cat $SRCS`" % (docker_build, run_args),
        srcs = [fqn],
        deps = [docker_build],
        visibility = visibility,
        labels = labels,
    )

    # docker push
    sh_cmd(
        name = name + "_push",
        cmd = [
            "./$(out_location %s) && docker push `cat $SRCS`;" % docker_build,
        ],
        srcs = [fqn],
        deps = [docker_build],
        visibility = visibility,
        labels = labels,
    )
    return docker_build

def _docker_tarball_rule(name, srcs, dockerfile, base_image, test_only):
    """Defines the tarball rule containing all required data for generating the container.

    The resulting tarball will contain the Dockerfile as well as any required artifacts.
    This will then later on be fed into `docker build` as a docker context.
    """
    if base_image:
        # Define rule to template the source image into the Dockerfile.
        if dockerfile.startswith("//"):
            _, _, label = dockerfile.partition(":")
            out = "Dockerfile-" + label
        else:
            out = dockerfile

        sanitized_base_image = "//" + base_image.split("//")[-1]

        dockerfile = build_rule(
            name = name,
            tag = "dockerfile",
            srcs = {
                "dockerfile": [dockerfile],
                "fqn": [base_image + "_fqn"],
            },
            outs = [out],
            cmd = 'FQN="`cat $SRCS_FQN`"; sed -e "s|%s|$FQN|g" $SRCS_DOCKERFILE >> $OUTS' % sanitized_base_image,
            test_only = test_only,
        )

    return tarball(
        name = "_%s#docker_context" % name,
        srcs = srcs + [dockerfile],
        test_only = test_only,
    ), dockerfile


_dockerfile = """
FROM //common/docker_containers:go_base

COPY __BINARY__ __BINARY__

__GRPC_HEALTH_PROBE__

__RESOURCES__
ENTRYPOINT ["./__BINARY__"]
"""

def distroless_go(
        name:str, binary:str, grpc_health_probe: bool=False, resources:list=[], visibility:list=[]
):
    grpc_health_probe_replace = ''
    if grpc_health_probe:
        target_name = get_target_name('//github.com/malonaz/core/go/grpc/grpc_health_probe')
        grpc_health_probe_replace = f'COPY {target_name} {target_name}'
    resources_replace = ''
    for resource in resources:
        target_name = get_target_name(resource)
        resources_replace += f'COPY {target_name} {target_name}'
    dockerfile = templated_text_file(
        filename = f'{name}_dockerfile',
        content = _dockerfile,
        replace = {
            '__BINARY__': get_target_name(binary),
            '__GRPC_HEALTH_PROBE__': grpc_health_probe_replace,
            '__RESOURCES__': resources_replace,
        },
    )

    return docker_image(
        name = name,
        srcs = [binary, '//github.com/malonaz/core/go/grpc/grpc_health_probe'] + resources,
        dockerfile = dockerfile,
        base_image = "//common/docker_containers:go_base",
        visibility = visibility,
    )
